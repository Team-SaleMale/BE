plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'sanhak'
version = '0.0.1-SNAPSHOT'
description = 'for 25-2 sanhak project'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	testRuntimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// JSON 처리 (Jackson)
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.fasterxml.jackson.core:jackson-annotations'

	// QueryDSL 추가
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	// Swagger 추가
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
}

tasks.named('test') {
	useJUnitPlatform()
}

// ===== QueryDSL 설정 시작 =====
// Q클래스 생성 위치 지정
def querydslDir = "src/main/generated"

// QueryDSL이 생성한 Q클래스를 소스로 인식하도록 설정
sourceSets {
	main.java.srcDirs += [ querydslDir ]
}

// JavaCompile 시 Q클래스 생성 위치 설정
tasks.withType(JavaCompile).configureEach {
	options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)
}

// clean 시 생성된 Q클래스도 삭제
clean {
	delete file(querydslDir)
}
// ===== QueryDSL 설정 끝 =====